apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation "junit:junit:${extJunit}"
    androidTestImplementation "androidx.test:runner:${extRunner}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${extEspressoCore}"

    implementation "androidx.constraintlayout:constraintlayout:${extConstraintLayout}"
    implementation "androidx.appcompat:appcompat:${extAppcompat}"
    implementation "androidx.recyclerview:recyclerview:${extRecyclerview}"

    //okhttph鸿洋的封装(https://github.com/hongyangAndroid/okhttputils)
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.alibaba:fastjson:1.2.51'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    //自动销毁rxjava 的线程
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.4.0'

}

//上传github 作为maven仓库
apply plugin: 'maven'


uploadArchives {
    repositories.mavenDeployer {
        repository(url: uri(rootProject.ext.maven_url))
        pom.project {
            groupId rootProject.ext.groupId// 唯一标识（通常为模块包名，也可以任意）
            artifactId project.name// 项目名称（通常为类库模块名称，也可以任意）
            version project.android.defaultConfig.versionName // 版本号
        }
    }
    //写入版本号
    def xmlFile = new File(uri(rootProject.ext.version_url))

    def xmlObj = new XmlParser().parse(xmlFile)

    println(">---wirte "+project.name+" version begin-------")
    println(">---file url:" + xmlFile.absolutePath)
    def oldVersion

    def versionObj = xmlObj.get(project.name)
    if (versionObj instanceof List) {
        List versionObjs = xmlObj.get(project.name)
        if (versionObjs.size() > 0) {
            //修改节点
            oldVersion = xmlObj.get(project.name)[0].attributes().get("value")
            xmlObj.get(project.name)[0].attributes().put("value", project.android.defaultConfig.versionName)

        } else {
            //新建节点
            oldVersion="0"
            Map<String, String> map = new HashMap<>()
            map.put("value", project.android.defaultConfig.versionName)
            xmlObj.appendNode(project.name, map)
        }
    }

    println(">---" + oldVersion + "  --> " + xmlObj.get(project.name)[0].attributes().get("value"))
    println(">---wirte lib version end-------")
    //写入文件
    new XmlNodePrinter(new PrintWriter(new FileWriter(xmlFile))).print(xmlObj)
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options.encoding = "UTF-8"
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}